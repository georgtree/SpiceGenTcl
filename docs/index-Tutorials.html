<!DOCTYPE html><html><head><meta charset="utf-8"/>
<title>Tcl SpiceGenTcl package</title>
<link rel='stylesheet' type='text/css' href='assets/ruff-min.css' />
<script type='text/javascript' src='assets/ruff-min.js'></script>
</head>
<body>
<div class='ruff-layout'>
<header class='ruff-layout-header ruff-hd'>
<a style='text-decoration:none;' href='index.html'>Tcl SpiceGenTcl package (v0.54)</a>


            <div id="ruffButtonBar">
            <button id="ruffNavMove" onclick="ruffMoveNavPane()"></button>
            <button id="ruffToggleTheme" onclick="ruffNextTheme()"></button>
            </div>
        </header><main class='ruff-layout-main ruff-bd'><h1 class='ruff'><a name='Tutorials'></a>Tutorials<span class='ruff-uplink'><a href='#top'>Top</a>, <a href='index.html'>Main</a>, <a href='index-docindex.html#'>Index</a></span></h1>
<div style='clear:both;'></div>
<p class='ruff'>Tutorials in this section describes examples from folder <code>examples</code> in root directory of SpiceGenTcl. List of availible tutorials:</p>
<ul class='ruff'>
<li><a href="index-Tutorials.html#::Tutorials-Netlist manipulations" title="Netlist manipulations" >Netlist manipulations</a> - &quot;examples/netlist_manipulations.tcl&quot; file</li>
<li><a href="index-Tutorials.html#::Tutorials-Subcircuit definition" title="Subcircuit definition" >Subcircuit definition</a> - &quot;examples/subcircuit.tcl&quot; file</li>
<li><a href="index-Tutorials.html#::Tutorials-Diode current simualtion" title="Diode current simualtion" >Diode current simualtion</a> - &quot;examples/ngspice/dc/diode_iv.tcl&quot; and &quot;examples/xyce/dc/diode_iv.tcl&quot; files.</li>
<li><a href="index-Tutorials.html#::Tutorials-Diode capacitance simualtion" title="Diode capacitance simualtion" >Diode capacitance simualtion</a> - &quot;examples/ngspice/ac/diode_cv.tcl&quot; and &quot;examples/xyce/ac/diode_cv.tcl&quot; files.</li>
<li><a href="index-Tutorials.html#::Tutorials-Sensitive analysis of differential pair" title="Sensitive analysis of differential pair" >Sensitive analysis of differential pair</a> - &quot;examples/ngspice/sens/diffpair_sens.tcl&quot; file</li>
<li><a href="index-Tutorials.html#::Tutorials-Transient simulation of ring oscillator" title="Transient simulation of ring oscillator" >Transient simulation of ring oscillator</a> - &quot;examples/ngspice/transient/switch_oscillator.tcl&quot; and &quot;examples/xyce/transient/switch_oscillator.tcl&quot; files.</li>
<li><a href="index-Tutorials.html#::Tutorials-Transient simulation of four-bit adder" title="Transient simulation of four-bit adder" >Transient simulation of four-bit adder</a> - &quot;examples/ngspice/transient/fourbitadder.tcl&quot; and &quot;examples/xyce/transient/fourbitadder.tcl&quot;files</li>
<li><a href="index-Tutorials.html#::Tutorials-S-parameter simulation of pass-band filter" title="S-parameter simulation of pass-band filter" >S-parameter simulation of pass-band filter</a> - &quot;examples/ngspice/sp/filter.tcl&quot;</li>
</ul>
<h2 class='ruff'><a name='::Tutorials-Netlist manipulations'></a>Netlist manipulations<span class='ruff-uplink'><a href='#top'>Top</a>, <a href='index.html'>Main</a>, <a href='index-docindex.html#'>Index</a></span></h2>
<div style='clear:both;'></div>
<p class='ruff'>In this example we will examine the availible actions that we can do on elements in netlist. First step is building the target netlist with variable elements:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# create netlist 
set netlist [Netlist new main_netlist]
# incrementally build netlist by adding different elements
$netlist add [R new 1 net1 net2 -r 10]
$netlist add [R new 5 net1 net2 -model res_sem -l 10e-6 -w 100e-6]
$netlist add [RModel new rsem1mod -tc1 0.1 -tc2 0.4]
$netlist add [R new 2 net1 net2 -r {{r1+5/10} -eq}]
$netlist add [C new 1 net2 net3 -c 1e-6]
$netlist add [ParamStatement new {{r1 1} {r2 2}} -name ps1]
$netlist add [Comment new {some random comment} -name com1]
$netlist add [Include new {/fold1/fold2/file.lib} -name inc1]
$netlist add [Library new {/fold1/fold2/file.lib} fast -name lib1]
$netlist add [RawString new {*comment in form of raw string} -name raw1]
$netlist add [Vdc new 1 net1 net3 -dc 5]
$netlist add [Tran new -tstep 1e-6 -tstop 1e-3 -uic -name tran1]
</pre>

</figure><p class='ruff'>Here we can see the <a href="index-SpiceGenTcl.html#::SpiceGenTcl::Netlist::add" title="::SpiceGenTcl::Netlist::add" class='ruff_cmd'>::SpiceGenTcl::Netlist::add</a> method that adds elemements objects references to <a href="index-SpiceGenTcl.html#::SpiceGenTcl::Netlist" title="::SpiceGenTcl::Netlist" class='ruff_cmd'>::SpiceGenTcl::Netlist</a> object. To view netlist we can invoke method <a href="index-SpiceGenTcl.html#::SpiceGenTcl::Netlist::genSPICEString" title="::SpiceGenTcl::Netlist::genSPICEString" class='ruff_cmd'>::SpiceGenTcl::Netlist::genSPICEString</a> that have all circuit elements:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# generate SPICE netlist
puts [$netlist genSPICEString]
</pre>

</figure><p class='ruff'>Result is:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
r1 net1 net2 10
r5 net1 net2 res_sem l=10e-6 w=100e-6
.model rsem1mod r(tc1=0.1 tc2=0.4)
r2 net1 net2 {r1+5/10}
c1 net2 net3 1e-6
.param r1=1 r2=2
*some random comment
.include /fold1/fold2/file.lib
.lib /fold1/fold2/file.lib fast
*comment in form of raw string
v1 net1 net3 5
.tran 1e-6 1e-3 uic
</pre>

</figure><h3 class='ruff'><a name='::Tutorials-Delete element'></a>Delete element<span class='ruff-uplink'><a href='#top'>Top</a>, <a href='index.html'>Main</a>, <a href='index-docindex.html#'>Index</a></span></h3>
<div style='clear:both;'></div>
<p class='ruff'>The opposite action is to delete with the <a href="index-SpiceGenTcl.html#::SpiceGenTcl::Netlist::del" title="::SpiceGenTcl::Netlist::del" class='ruff_cmd'>::SpiceGenTcl::Netlist::del</a> method. For example, we can delete the 'c1' element from the netlist by specifying its name:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
$netlist del c1
puts [$netlist genSPICEString]
</pre>

</figure><p class='ruff'>Result is:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
r1 net1 net2 10
r5 net1 net2 res_sem l=10e-6 w=100e-6
.model rsem1mod r(tc1=0.1 tc2=0.4)
r2 net1 net2 {r1+5/10}
.param r1=1 r2=2
*some random comment
.include /fold1/fold2/file.lib
.lib /fold1/fold2/file.lib fast
*comment in form of raw string
v1 net1 net3 5
.tran 1e-6 1e-3 uic
</pre>

</figure><p class='ruff'>As you can see, capacitor 'c1' is no more in netlist.</p>
<h3 class='ruff'><a name='::Tutorials-Get element'></a>Get element<span class='ruff-uplink'><a href='#top'>Top</a>, <a href='index.html'>Main</a>, <a href='index-docindex.html#'>Index</a></span></h3>
<div style='clear:both;'></div>
<p class='ruff'>Next important operation is getting reference of element in netlist. We can do it again by specifying its name:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# get reference of resistor object
set resistor [$netlist getElement r1]
</pre>

</figure><p class='ruff'>After it, we can change the resistance parameter of element:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# change the value of resistor in circuit
$resistor setParamValue r 100
</pre>

</figure><p class='ruff'>We apply method <a href="index-SpiceGenTcl.html#::SpiceGenTcl::Device::setParamValue" title="::SpiceGenTcl::Device::setParamValue" class='ruff_cmd'>::SpiceGenTcl::Device::setParamValue</a> with two arguments: name of parameter 'r' and its new value '100'. The other action we can do is change the pin connection of element:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# change the name of connected net to plus pin
$resistor setPinNodeName np net10
</pre>

</figure><p class='ruff'>We use method <a href="index-SpiceGenTcl.html#::SpiceGenTcl::Device::setPinNodeName" title="::SpiceGenTcl::Device::setPinNodeName" class='ruff_cmd'>::SpiceGenTcl::Device::setPinNodeName</a> with pin name and name of the net as arguments.</p>
<p class='ruff'>Then we can again print netlist and see that value of parameter and name of connected name have been changed:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
r1 net10 net2 100
r5 net1 net2 res_sem l=10e-6 w=100e-6
.model rsem1mod r(tc1=0.1 tc2=0.4)
r2 net1 net2 {r1+5/10}
.param r1=1 r2=2
*some random comment
.include /fold1/fold2/file.lib
.lib /fold1/fold2/file.lib fast
*comment in form of raw string
v1 net1 net3 5
.tran 1e-6 1e-3 uic
</pre>

</figure><h3 class='ruff'><a name='::Tutorials-Print list of all elements'></a>Print list of all elements<span class='ruff-uplink'><a href='#top'>Top</a>, <a href='index.html'>Main</a>, <a href='index-docindex.html#'>Index</a></span></h3>
<div style='clear:both;'></div>
<p class='ruff'>We can get the list of all elements attached to <code>$netlist</code> object:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# get and print all names of elements attached to $netlist
puts [$netlist getAllElemNames]
</pre>

</figure><p class='ruff'>Result:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
r1 r5 rsem1mod r2 ps1 com1 inc1 lib1 raw1 v1 tran1
</pre>

</figure><h3 class='ruff'><a name='::Tutorials-Temporarly remove of element'></a>Temporarly remove of element<span class='ruff-uplink'><a href='#top'>Top</a>, <a href='index.html'>Main</a>, <a href='index-docindex.html#'>Index</a></span></h3>
<div style='clear:both;'></div>
<p class='ruff'>We can delete element from netlist and store reference to elements object, and then again add element to netlist:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
set r1 [$netlist getElement r1]
$netlist del r1
$netlist add $r1
</pre>

</figure><p class='ruff'>Also, we can save reference to object before adding to netlist in variable and only then add it to netlist:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
set r5 [R new 5 net1 net2 -r 10]
$netlist add $r5
</pre>

</figure><p class='ruff'>In this case we can directly modify 'r5' parameters without necessity to call <a href="index-SpiceGenTcl.html#::SpiceGenTcl::Netlist::getElement" title="::SpiceGenTcl::Netlist::getElement" class='ruff_cmd'>::SpiceGenTcl::Netlist::getElement</a> method.</p>
<h2 class='ruff'><a name='::Tutorials-Subcircuit definition'></a>Subcircuit definition<span class='ruff-uplink'><a href='#top'>Top</a>, <a href='index.html'>Main</a>, <a href='index-docindex.html#'>Index</a></span></h2>
<div style='clear:both;'></div>
<p class='ruff'>To create subcircuit definition we use special <a href="index-SpiceGenTcl.html#::SpiceGenTcl::Subcircuit" title="::SpiceGenTcl::Subcircuit" class='ruff_cmd'>::SpiceGenTcl::Subcircuit</a> class and make subcircuit by defining new class with it as superclass:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
oo::class create RCnet {
    superclass Subcircuit
    constructor {} {
        # define external pins of subcircuit
        set pins {plus minus}
        # define input parameters of subcircuit
        set params {{r 100} {c 1e-6}}
        # add elements to subcircuit definition
        my add [R new 1 net1 plus -r {r -eq}]
        my add [C new 1 net2 net3 -r {c -eq}]
        my add [R new 5 minus net2 -model res_sem -l 10e-6 -w 100e-6]
        my add [RModel new rsem1mod -tc1 0.1 -tc2 0.4]
        # pass name, list of pins and list of parameters to Subcircuit constructor
        next rcnet $pins $params
    }
}
</pre>

</figure><p class='ruff'>In constructor of this class we define pins as list with names in order of appearance in subcircuit header:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
set pins {plus minus}
</pre>

</figure><p class='ruff'>Then we define parameters as list of two-elements list that contains name and default value of parameter:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
set params {{r 100} {c 1e-6}}
</pre>

</figure><p class='ruff'>Next step is adding elements to subcircuit:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
my add [R new 1 net1 plus -r {r -eq}]
my add [C new 1 net2 net3 -c {c -eq}]
my add [R new 5 minus net2 -model res_sem -l 10e-6 -w 100e-6]
my add [RModel new rsem1mod -tc1 0.1 -tc2 0.4]
</pre>

</figure><p class='ruff'>The last action is to pass name of subcircuit, list of pins and parameters to constructor of superclass:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
next rcnet $pins $params
</pre>

</figure><p class='ruff'>Name passed to superclass constructor <code>rcnet</code> is not necessarily the same as name of the class, this name will be printed in subcircuit definition in netlist.</p>
<p class='ruff'>To create and add this definition to netlist, we use name of the class <code>RCnet</code>:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# create subcircuit definition
set subcircuit [RCnet new]
# add to netslit
$netlist add $subcircuit
</pre>

</figure><p class='ruff'>But place definition of subcircuit is not enough - we need to place netlist instance of subcircuit. We can do it by using two mechanisms:</p>
<ul class='ruff'>
<li>use <a href="index-SpiceGenTcl-Ngspice-BasicDevices.html#::SpiceGenTcl::Ngspice::BasicDevices::SubcircuitInstance" title="::SpiceGenTcl::Ngspice::BasicDevices::SubcircuitInstance" class='ruff_cmd'>::SpiceGenTcl::Ngspice::BasicDevices::SubcircuitInstance</a> class</li>
<li>use <a href="index-SpiceGenTcl-Ngspice-BasicDevices.html#::SpiceGenTcl::Ngspice::BasicDevices::SubcircuitInstanceAuto" title="::SpiceGenTcl::Ngspice::BasicDevices::SubcircuitInstanceAuto" class='ruff_cmd'>::SpiceGenTcl::Ngspice::BasicDevices::SubcircuitInstanceAuto</a> class</li>
</ul>
<p class='ruff'>First way is direct construction of element by providing pins and parameter lists:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# create subcircuit instance
set subInst [SubcircuitInstance new 1 {{plus net1} {minus net2}} rcnet {{r 1} {c cpar -eq}}]
</pre>

</figure><p class='ruff'>But the second way is simpler and allow to check if we have mistake in definition:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# create subcircuit instance with help of already created subcircuit definition $subcircuit
set subInst1 [SubcircuitInstanceAuto new $subcircuit 2 {net1 net2} -r 1 -c {cpar -eq}]
</pre>

</figure><p class='ruff'>In this approach, we pass the reference of our subcircuit as the first argument. Then, we only need to provide a list of nets connected to the pins, in the order defined in the subcircuit, and parameters in the form of <code>-paramName paramValue</code>. If you try to add a parameter that does not exist in the subcircuit definition, you'll receive an error.</p>
<p class='ruff'>The final circuit is:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
.subckt rcnet plus minus r=100 c=1e-6
r1 net1 plus {r}
c1 net2 net3 {c}
r5 minus net2 res_sem l=10e-6 w=100e-6
.model rsem1mod r(tc1=0.1 tc2=0.4)
.ends
x1 net1 net2 rcnet r=1 c={cpar}
x2 net1 net2 rcnet r=1 c={cpar}
</pre>

</figure><h2 class='ruff'><a name='::Tutorials-Diode current simualtion'></a>Diode current simualtion<span class='ruff-uplink'><a href='#top'>Top</a>, <a href='index.html'>Main</a>, <a href='index-docindex.html#'>Index</a></span></h2>
<div style='clear:both;'></div>
<p class='ruff'>In this example, we examine the parametric simulation of a diode model's current curve, with the ambient temperature as the parameter. To achieve this, we need to run the simulation multiple times at different temperatures, save the results from each iteration, and then combine them into a single plot.</p>
<p class='ruff'>The circuit is simple:</p>
<p class='ruff'><img src="assets/img/diode_iv_cir.png" alt="drawing" width="400"/></p>
<p class='ruff'>As in previous examples, we start by creating the top circuit and adding elements to it:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# create top-level circuit
set circuit [Circuit new {diode IV}]
# add elements to circuit
$circuit add [D new 1 anode 0 -model diomod -area 1 -lm 1e-6]
$circuit add [Vdc new a anode 0 -dc 0]
$circuit add [DiodeModel new diomod -is 1e-12 -n 1.2 -rs 0.01 -cj0 1e-9 -trs1 0.001 -xti 5]
$circuit add [Dc new -src va -start 0 -step 2 -incr 0.01]
</pre>

</figure><p class='ruff'>In the upper block of code, we add a diode instance using the <a href="index-SpiceGenTcl-Ngspice-SemiconductorDevices.html#::SpiceGenTcl::Ngspice::SemiconductorDevices::D" title="::SpiceGenTcl::Ngspice::SemiconductorDevices::D" class='ruff_cmd'>::SpiceGenTcl::Ngspice::SemiconductorDevices::D</a> command, create the corresponding diode model with the <a href="index-SpiceGenTcl-Ngspice-SemiconductorDevices.html#::SpiceGenTcl::Ngspice::SemiconductorDevices::DiodeModel" title="::SpiceGenTcl::Ngspice::SemiconductorDevices::DiodeModel" class='ruff_cmd'>::SpiceGenTcl::Ngspice::SemiconductorDevices::DiodeModel</a> command, add a DC voltage source that will be swept, and include a DC analysis statement. The voltage ranges from 0 to 2 volts to obtain the forward current characteristic.</p>
<p class='ruff'>The temperature in Ngspice is set using the <code>.temp</code> statement. We create an instance with the <a href="index-SpiceGenTcl.html#::SpiceGenTcl::Temp" title="::SpiceGenTcl::Temp" class='ruff_cmd'>::SpiceGenTcl::Temp</a> command, save the object reference in the variable <code>tempSt</code>, and then add it to the circuit:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# add .temp
set tempSt [Temp new 25]
$circuit add $tempSt
</pre>

</figure><p class='ruff'>Next, we create an array containing the temperature values and instantiate an object of the <a href="index-SpiceGenTcl-Ngspice-Simulators.html#::SpiceGenTcl::Ngspice::Simulators::Batch" title="::SpiceGenTcl::Ngspice::Simulators::Batch" class='ruff_cmd'>::SpiceGenTcl::Ngspice::Simulators::Batch</a> class.:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# add temperature sweep
set temps [list -55 25 85 125 175]
# set simulator with default 
set simulator [Batch new {batch1}]
# attach simulator object to circuit
$circuit configure -Simulator $simulator
</pre>

</figure><p class='ruff'>The only thing left is to set the temperature value in the <a href="index-SpiceGenTcl.html#::SpiceGenTcl::Temp" title="::SpiceGenTcl::Temp" class='ruff_cmd'>::SpiceGenTcl::Temp</a> class object, run the circuit, save the data, and repeat this process multiple times:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# run circuit, change temperature, read log and data
foreach temp $temps {
    $tempSt configure -Value $temp
    $circuit runAndRead
    puts [$circuit getLog]
    set data [$circuit getDataDict]
    foreach x [dget $data v(anode)] y [dget $data i(va)]   {
        set xf [format &quot;%.3f&quot; $x]
        set yf [format &quot;%.3f&quot; [= {-$y}]]
        lappend xydata [list $xf $yf]
    }    
    lappend dataList $xydata
    unset xydata
}
</pre>

</figure><p class='ruff'>Plotting the results:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# plot results with ticklecharts
set chart [ticklecharts::chart new]
$chart Xaxis -name &quot;v(anode), V&quot; -minorTick {show &quot;True&quot;}  -type &quot;value&quot;
$chart Yaxis -name &quot;Idiode, A&quot; -minorTick {show &quot;True&quot;}  -type &quot;value&quot;
$chart SetOptions -title {} -tooltip {} -animation &quot;False&quot; -legend  {}  -toolbox {feature {dataZoom {yAxisIndex &quot;none&quot;}}} -grid {left &quot;5%&quot; right &quot;15%&quot;}
foreach data $dataList temp $temps {
    $chart Add &quot;lineSeries&quot; -data $data -showAllSymbol &quot;nothing&quot; -name &quot;${temp}°C&quot; -symbolSize &quot;1&quot;
}
set fbasename [file rootname [file tail [info script]]]

$chart Render -outfile [file normalize [file join html_charts $fbasename.html]]
</pre>

</figure><p class='ruff'>In the picture, you can see how temperature affects the forward current of the diode. Because the series resistance has a temperature dependence, all the lines intersect near 1.8V, indicating the point of zero temperature coefficient:</p>
<p class='ruff'><img src="assets/img/diode_iv.png" alt="diode_iv" /></p>
<p class='ruff'>This circuit works almost without modification in Xyce simulator, see &quot;examples/xyce/dc/diode_iv.tcl&quot; file. The one of the differences is how we set the global temperature: in Xyce there is no .temp statement, for setting global temperature we use <code>.options</code> statement with class <a href="index-SpiceGenTcl.html#::SpiceGenTcl::Options" title="::SpiceGenTcl::Options" class='ruff_cmd'>::SpiceGenTcl::Options</a>:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
set tempSt [Options new {{device -sw} {temp 25}}]
</pre>

</figure><p class='ruff'>and then modify this value in loop:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
$tempSt setParamValue temp $temp
</pre>

</figure><h2 class='ruff'><a name='::Tutorials-Diode capacitance simualtion'></a>Diode capacitance simualtion<span class='ruff-uplink'><a href='#top'>Top</a>, <a href='index.html'>Main</a>, <a href='index-docindex.html#'>Index</a></span></h2>
<div style='clear:both;'></div>
<p class='ruff'>In this example, we measure the volt-farad (or capacitance-voltage) relationship of the diode's depletion charge. To achieve this, we need to apply a negative voltage bias to the anode and perform an AC simulation at a single frequency. We then repeat this process at different voltages, collect all the results, and finally plot the entire curve. We calculate the capacitance value using the following equation:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
          ⎛I⎞   
      -Im ⎜─⎟   
          ⎝V⎠   
C = ────────────
    2 ⋅ π ⋅ freq

</pre>

</figure><p class='ruff'>where I and V - voltage and current phasors, freq - frequency of applied AC signal. First, we can define π constant manually, or borrow the value from <code>::math::constants</code> library:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
package require math::constants
::math::constants::constants  pi
</pre>

</figure><p class='ruff'>We again go through the circuit building sequence:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# create top-level circuit
set circuit [Circuit new {diode CV}]
# add elements to circuit
$circuit add [D new 1 0 c -model diomod -area 1 -lm 1e-6]
set vdc [Vdc new a c nin -dc 0]
$circuit add $vdc
$circuit add [Vac new b nin 0 -ac 1]
$circuit add [DiodeModel new diomod -is 1e-12 -n 1.2 -rs 0.01 -cj0 1e-9 -trs1 0.001 -xti 5]
$circuit add [Ac new -variation lin -n 1 -fstart 1e5 -fstop 1e5]
</pre>

</figure><p class='ruff'>What are new here:</p>
<ul class='ruff'>
<li>AC voltage source <code>[Vac new b nin 0 -ac 1]</code> - uses to generate AC signal</li>
<li>AC analysis statement <code>[Ac new -variation lin -n 1 -fstart 1e5 -fstop 1e5]</code> - uses to define AC frequency, 100 kHz in our case.</li>
</ul>
<p class='ruff'>The final circuit looks like this:</p>
<p class='ruff'><img src="assets/img/diode_cv_cir.png" alt="drawing" width="500"/></p>
<p class='ruff'>Nest step is to add voltage sweep:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# add voltage sweep
set voltSweep [lseq 0 20.0 0.1]
</pre>

</figure><p class='ruff'>Then we as usual, create <a href="index-SpiceGenTcl-Ngspice-Simulators.html#::SpiceGenTcl::Ngspice::Simulators::BatchLiveLog" title="::SpiceGenTcl::Ngspice::Simulators::BatchLiveLog" class='ruff_cmd'>::SpiceGenTcl::Ngspice::Simulators::BatchLiveLog</a> class object, attach it to <code>$circuit</code>, run it multiple times, collect results and apply the equation to calculate the capacitance (with the voltage phasor set to 1, so it is omitted):</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
#set simulator with default 
set simulator [BatchLiveLog new {batch1}]
# attach simulator object to circuit
$circuit configure -Simulator $simulator
# loop in which we run simulation, change reverse bias and read the results
foreach volt $voltSweep {
    #set reverse voltage bias
    $vdc setParamValue dc $volt
    # run simulation
    $circuit runAndRead
    # get data object
    set data [$circuit getDataDict]
    set x $volt
    # get imaginary part of current
    set y [@ [dget $data i(va)] 0 1]
    set xf [format &quot;%.3e&quot; $x]
    set yf [format &quot;%.2e&quot; [= {-$y/(2*$pi*1e5*1e-9)}]]
    lappend xydata [list $xf $yf]
}
</pre>

</figure><p class='ruff'>The <a href="index-SpiceGenTcl-Ngspice-Simulators.html#::SpiceGenTcl::Ngspice::Simulators::BatchLiveLog" title="::SpiceGenTcl::Ngspice::Simulators::BatchLiveLog" class='ruff_cmd'>::SpiceGenTcl::Ngspice::Simulators::BatchLiveLog</a> simulator differs from <a href="index-SpiceGenTcl-Ngspice-Simulators.html#::SpiceGenTcl::Ngspice::Simulators::Batch" title="::SpiceGenTcl::Ngspice::Simulators::Batch" class='ruff_cmd'>::SpiceGenTcl::Ngspice::Simulators::Batch</a> in that it prints output line by line during the simulation, rather than only at the end.</p>
<p class='ruff'>Here, we collect the imaginary component of the current flowing through the AC voltage source 'Va'.</p>
<p class='ruff'>You might wonder why we use two indices, 0 and 1, when extracting values from the vector i(va).</p>
<p class='ruff'>The answer is as follows: the first index extracts values from the list of values at different frequencies, while the second index retrieves the imaginary component.</p>
<p class='ruff'>The AC data vectors are organized as follows:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
{{reVal0 imVal0} {reVal1 imVal1} {reVal2 imVal2} ...}
</pre>

</figure><p class='ruff'>Here, <code>reVal0</code> is the real value at frequency index 0, <code>imVal0</code>is the imaginary value at the same frequency, <code>reVal1</code> is the real value at frequency index 1, <code>imVal1</code> is the imaginary value at that frequency, and so on. Even if there is only one frequency in the AC analysis, you should still extract the value using index 0.</p>
<p class='ruff'>Now we can plot the results:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# plot results with ticklecharts
set chart [ticklecharts::chart new]
$chart Xaxis -name &quot;v(0,c), V&quot; -minorTick {show &quot;True&quot;} -type &quot;value&quot;
$chart Yaxis -name &quot;Diode capacitance, nF&quot; -minorTick {show &quot;True&quot;} -type &quot;value&quot;
$chart SetOptions -title {} -tooltip {} -animation &quot;False&quot; -toolbox {feature {dataZoom {yAxisIndex &quot;none&quot;}}}
$chart Add &quot;lineSeries&quot; -data $xydata -showAllSymbol &quot;nothing&quot;
set fbasename [file rootname [file tail [info script]]]

$chart Render -outfile [file normalize [file join html_charts $fbasename.html]]
</pre>

</figure><p class='ruff'>As a result, we obtain the expected curve, showing a decrease in capacitance value with higher reverse voltage:</p>
<p class='ruff'><img src="assets/img/diode_cv.png" alt="diode_cv" /></p>
<h2 class='ruff'><a name='::Tutorials-Sensitive analysis of differential pair'></a>Sensitive analysis of differential pair<span class='ruff-uplink'><a href='#top'>Top</a>, <a href='index.html'>Main</a>, <a href='index-docindex.html#'>Index</a></span></h2>
<div style='clear:both;'></div>
<p class='ruff'>In this circuit we run DC sensitive analysis of differential pair:</p>
<p class='ruff'><img src="assets/img/diff_pair_cir.png" alt="drawing" width="700"/></p>
<p class='ruff'>We build circuit step by step:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# create top-level circuit
set circuit [Circuit new {simple differential pair}]
# add elements to circuit
$circuit add [Vdc new cc 8 0 -dc 12]
$circuit add [Vdc new ee 9 0 -dc -12]
$circuit add [Vac new cm 1 0 -ac 1]
$circuit add [Vac new dm 1 11 -ac 1]
$circuit add [Q new 1 4 2 6 -model qnr]
$circuit add [Q new 2 5 3 6 -model qnl]
$circuit add [R new s1 11 2 -r 1e3]
$circuit add [R new s2 3 1 -r 1e3]
$circuit add [R new c1 4 8 -r 10e3]
$circuit add [R new c2 5 8 -r 10e3]
$circuit add [Q new 3 7 7 9 -model qnl]
$circuit add [Q new 4 6 7 9 -model qnr]
$circuit add [R new bias 7 8 -r 20e3]
</pre>

</figure><p class='ruff'>Then we add models for NPN and PNP bipolar transistors:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
$circuit add [BjtGPModel new qnl npn -bf 80 -rb 100 -cjc 2e-12 -tf 0.3e-9 -tr 6e-9 -cje 3e-12 -cjc 2e-12 -va 50]
$circuit add [BjtGPModel new qnr npn -bf 80 -rb 100 -cjc 2e-12 -tf 0.3e-9 -tr 6e-9 -cje 3e-12 -cjc 2e-12 -va 50]
</pre>

</figure><p class='ruff'>In DC sensistive analysis we add input voltage to which we sense ohter parameters in the circuit:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
$circuit add [SensDc new -outvar v(5,4)]
</pre>

</figure><p class='ruff'>We create simulator, run it and read data:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
#set simulator with default 
set simulator [Batch new {batch1}]
# attach simulator object to circuit
$circuit configure -Simulator $simulator
# run circuit, read log and data
$circuit runAndRead
# get data object
set data [$circuit getDataDict]
set vrc1 [dget $data v(rc1)]
set vrc2 [dget $data v(rc2)]
</pre>

</figure><p class='ruff'>To print resulted sensitivities we use <code>format</code> command where we specify the format of the number:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
vrc1=6.032e-04 
vrc2=-6.032e-04
</pre>

</figure><h2 class='ruff'><a name='::Tutorials-Transient simulation of ring oscillator'></a>Transient simulation of ring oscillator<span class='ruff-uplink'><a href='#top'>Top</a>, <a href='index.html'>Main</a>, <a href='index-docindex.html#'>Index</a></span></h2>
<div style='clear:both;'></div>
<p class='ruff'>In this example, we analyze a circuit with transient analysis and demonstrate how to use a Tcl script to build a circuit containing multiple stages of the same subcircuit.</p>
<p class='ruff'>The circuit is a ring oscillator composed of voltage-controlled switches, based on an example from Ngspice (/examples/p-to-n-examples/switch-oscillators.cir). Each stage of the oscillator is a simple two-switch inverter, and the entire circuit consists of 17 stages.</p>
<p class='ruff'>At the beginning, we create a class called <code>Inverter</code> to describe the inverter and then instantiate an object of this class:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# create class that represents inverter subcircuit
oo::class create Inverter {
    superclass Subcircuit
    constructor {} {
        # define external pins of subcircuit
        set pins {in out vdd dgnd}
        # define input parameters of subcircuit
        set params {}
        # add elements to subcircuit definition
        my add [C new l out dgnd -c 0.1e-12]
        my add [C new 2 out vdd -c 0.1e-12]
        my add [VSwitch new p out vdd vdd in -model swswitch]
        my add [VSwitch new n out dgnd in dgnd -model switchn]
        # pass name, list of pins and list of parameters to Subcircuit constructor
        next inverter $pins $params
    }
}
# create subcircuit definition instance
set inverter [Inverter new]
</pre>

</figure><p class='ruff'>Next, we define the other elements and the top-level circuit:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# create top-level circuit
set circuit [Circuit new {switch_oscillator}]
# add elements to circuit
$circuit add [Tran new -tstep 50e-12 -tstop 80e-9]
$circuit add [Options new {{method gear} {maxord 3}}]
$circuit add [RawString new &quot;.ic v(osc_out)=0.25&quot;]
$circuit add $inverter
$circuit add [Vdc new dd vdd2 0 -dc 3]
$circuit add [Vdc new measure vdd2 vdd -dc 0]
$circuit add [C new vdd vdd 0 -c 1e-18]
</pre>

</figure><p class='ruff'>We can add multiple stages of inverters using a loop, which saves many lines of code and allows us to dynamically adjust the number of stages:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# add multiple inverters in the cycle
for {set i 1} {$i&lt;16} {incr i} {
    set ip1 [+ $i 1]
    $circuit add [SubcircuitInstanceAuto new $inverter x${ip1} &quot;n${i} n${ip1} vdd 0&quot;]
}
</pre>

</figure><p class='ruff'>After that, we add the first and last stages, as well as the models of the switches:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
$circuit add [SubcircuitInstanceAuto new $inverter x18 {osc_out n1 vdd 0}]
$circuit add [SubcircuitInstanceAuto new $inverter x19 {n16 osc_out vdd 0}]
$circuit add [VSwitchModel new swswitch -vt 1 -vh 0.1 -ron 1e3 -roff 1e12]
$circuit add [VSwitchModel new switchn -vt 1 -vh 0.1 -ron 1e3 -roff 1e12]
</pre>

</figure><p class='ruff'>Now we can run and read data:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
#set simulator with default temporary directory
set simulator [BatchLiveLog new {batch1}]
# attach simulator object to circuit
$circuit configure -Simulator $simulator
# run circuit, read log and data
$circuit runAndRead
# get data object
set data [$circuit getDataDict]
set axis [dget $data time]
set vout [dget $data v(osc_out)]
set imeas [dget $data i(vmeasure)]
foreach time [dget $data time] vout [dget $data v(osc_out)] imeas [dget $data i(vmeasure)] {
    lappend timeVout [list $time $vout]
    lappend timeImeas [list $time $imeas]
}
</pre>

</figure><p class='ruff'>We save output waveform and power currents, and then plot it:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# plot results with ticklecharts
# chart for output voltage
set chartVout [ticklecharts::chart new]
$chartVout Xaxis -name &quot;time, s&quot; -minorTick {show &quot;True&quot;} -type &quot;value&quot;
$chartVout Yaxis -name &quot;Output voltage, V&quot; -minorTick {show &quot;True&quot;} -type &quot;value&quot;
$chartVout SetOptions -title {} -tooltip {} -animation &quot;False&quot; -toolbox {feature {dataZoom {yAxisIndex &quot;none&quot;}}}
$chartVout Add &quot;lineSeries&quot; -data $timeVout -showAllSymbol &quot;nothing&quot; -symbolSize &quot;0&quot;
# chart for measured current
set chartImeas [ticklecharts::chart new]
$chartImeas Xaxis -name &quot;time, s&quot; -minorTick {show &quot;True&quot;} -type &quot;value&quot;
$chartImeas Yaxis -name &quot;Current, I&quot; -minorTick {show &quot;True&quot;} -type &quot;value&quot;
$chartImeas SetOptions -title {} -tooltip {} -animation &quot;False&quot;  -toolbox {feature {dataZoom {yAxisIndex &quot;none&quot;}}}
$chartImeas Add &quot;lineSeries&quot; -data $timeImeas -showAllSymbol &quot;nothing&quot; -symbolSize &quot;0&quot;
# create multiplot
set layout [ticklecharts::Gridlayout new]
$layout Add $chartVout -bottom &quot;5%&quot; -height &quot;40%&quot; -width &quot;80%&quot;
$layout Add $chartImeas -bottom &quot;55%&quot; -height &quot;40%&quot; -width &quot;80%&quot;

set fbasename [file rootname [file tail [info script]]]
$layout Render -outfile [file normalize [file join .. html_charts $fbasename.html]] -height 800px
</pre>

</figure><p class='ruff'><img src="assets/img/switch_oscillator.png" alt="switch_oscillator" /></p>
<h2 class='ruff'><a name='::Tutorials-Transient simulation of four-bit adder'></a>Transient simulation of four-bit adder<span class='ruff-uplink'><a href='#top'>Top</a>, <a href='index.html'>Main</a>, <a href='index-docindex.html#'>Index</a></span></h2>
<div style='clear:both;'></div>
<p class='ruff'>This example, like the previous one, involves a circuit run in transient analysis. However, it differs in terms of complexity and the presence of nested subcircuit elements.</p>
<p class='ruff'>The circuit is a classic adder with 2 four-bit inputs, sourced from Ngspice tests (/tests/transient/fourbitadder.cir). We incrementally build it from the simplest NAND logic blocks up to the four-bit adder circuit.</p>
<p class='ruff'>The NAND block itself is constructed from a combination of bipolar transistors, diodes, and resistors. Its definition is as follows:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# create class that represents NAND subcircuit
oo::class create NAND {
    superclass Subcircuit
    constructor {} {
        # define external pins of subcircuit
        set pins {1 2 3 4}
        # define input parameters of subcircuit
        set params {}
        # add elements to subcircuit definition
        my add [Q new 1 9 5 1 -model qmod]
        my add [D new 1clamp 0 1 -model dmod]
        my add [Q new 2 9 5 2 -model qmod]
        my add [D new 2clamp 0 2 -model dmod]
        my add [R new b 4 5 -r 4e3]
        my add [R new 1 4 6 -r 1.6e3]
        my add [Q new 3 6 9 8 -model qmod]
        my add [R new 2 8 0 -r 1e3]
        my add [R new c 4 7 -r 130]
        my add [Q new 4 7 6 10 -model qmod]
        my add [D new vbedrop 10 3 -model dmod]
        my add [Q new 5 3 8 0 -model qmod]
        # pass name, list of pins and list of parameters to Subcircuit constructor
        next nand $pins $params
    }
}
# create NAND subcircuit definition instance
set nand [NAND new]
</pre>

</figure><p class='ruff'>To build one-bit adder we use combination of NAND subcircuits:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# create class that represents ONEBIT subcircuit
oo::class create ONEBIT {
    superclass Subcircuit
    constructor {} {
        # define external pins of subcircuit
        set pins {1 2 3 4 5 6}
        # define input parameters of subcircuit
        set params {}
        # add elements to subcircuit definition
        global variable nand
        my add [XAuto new $nand x1 &quot;1 2 7 6&quot;]
        my add [XAuto new $nand x2 &quot;1 7 8 6&quot;]
        my add [XAuto new $nand x3 &quot;2 7 9 6&quot;]
        my add [XAuto new $nand x4 &quot;8 9 10 6&quot;]
        my add [XAuto new $nand x5 &quot;3 10 11 6&quot;]
        my add [XAuto new $nand x6 &quot;3 11 12 6&quot;]
        my add [XAuto new $nand x7 &quot;10 11 13 6&quot;]
        my add [XAuto new $nand x8 &quot;12 13 4 6&quot;]
        my add [XAuto new $nand x9 &quot;11 7 5 6&quot;]
        # pass name, list of pins and list of parameters to Subcircuit constructor
        next onebit $pins $params
    }
}
# create ONEBIT subcircuit definition instance
set onebit [ONEBIT new]
</pre>

</figure><p class='ruff'>Previously, we built subcircuits, but here we demonstrate that subcircuits can be used within the definitions of other subcircuits, allowing us to create a multi-level hierarchy for building complex circuits. We start by combining one-bit adders to form two-bit adders, and then increase the level of grouping by connecting two two-bit subcircuits to create a four-bit adder:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# create class that represents TWOBIT subcircuit
oo::class create TWOBIT {
    superclass Subcircuit
    constructor {} {
        # define external pins of subcircuit
        set pins {1 2 3 4 5 6 7 8 9}
        # define input parameters of subcircuit
        set params {}
        # add elements to subcircuit definition
        global variable onebit
        my add [XAuto new $onebit x1 &quot;1 2 7 5 10 9&quot;]
        my add [XAuto new $onebit x2 &quot;3 4 10 6 8 9&quot;]
        # pass name, list of pins and list of parameters to Subcircuit constructor
        next twobit $pins $params
    }
}
# create TWOBIT subcircuit definition instance
set twobit [TWOBIT new]
# create class that represents FOURBIT subcircuit
oo::class create FOURBIT {
    superclass Subcircuit
    constructor {} {
        # define external pins of subcircuit
        set pins {1 2 3 4 5 6 7 8 9 10 11 12 13 14 15}
        # define input parameters of subcircuit
        set params {}
        # add elements to subcircuit definition
        global variable twobit
        my add [XAuto new $twobit x1 &quot;1 2 3 4 9 10 13 16 15&quot;]
        my add [XAuto new $twobit x2 &quot;5 6 7 8 11 12 16 14 15&quot;]
        # pass name, list of pins and list of parameters to Subcircuit constructor
        next fourbit $pins $params
    }
}
# create FOURBIT subcircuit definition instance
set fourbit [FOURBIT new]
</pre>

</figure><p class='ruff'>Next step is to initialize top circuit and add all subcircuit definitions to it:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# create top-level circuit
set circuit [Circuit new {Four-bit adder}]
# add elements to circuit
$circuit add [Tran new -tstep 1e-9 -tstop 10e-6]
$circuit add [Options new {{noacct -sw}}]
$circuit add $nand
$circuit add $onebit
$circuit add $twobit
$circuit add $fourbit
$circuit add [XAuto new $fourbit x18 {1 2 3 4 5 6 7 8 9 10 11 12 0 13 99}]
</pre>

</figure><p class='ruff'>To properly see the circuit in action we define input signals to all eight inputs, as well as power:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
set trtf 10e-9
set tonStep 10e-9
set perStep 50e-9
$circuit add [Vdc new cc 99 0 -dc 5]
set i 1
foreach name [list in1a in1b in2a in2b in3a in3b in4a in4b] {
    $circuit add [Vpulse new $name $i 0 -low 0 -high 3 -td 0 -tf $trtf -tr $trtf -pw [= {$tonStep*pow(2,$i-1)}] -per [= {$perStep*pow(2,$i-1)}]]
    incr i
}
</pre>

</figure><p class='ruff'>Add load resistors:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
$circuit add [R new bit0 9 0 -r 1e3]
$circuit add [R new bit1 10 0 -r 1e3]
$circuit add [R new bit2 11 0 -r 1e3]
$circuit add [R new bit3 12 0 -r 1e3]
$circuit add [R new cout 13 0 -r 1e3]
</pre>

</figure><p class='ruff'>Add semiconductor devices models:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
$circuit add [DiodeModel new dmod]
$circuit add [BjtGPModel new qmod npn -bf 75 -rb 100 -cje 1e-12 -cjc 3e-12]
</pre>

</figure><p class='ruff'>Now we can create simulator and run circuit:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
#set simulator with default temporary directory
set simulator [BatchLiveLog new {batch1}]
# attach simulator object to circuit
$circuit configure -Simulator $simulator
# run circuit, read log and data
$circuit runAndRead
</pre>

</figure><p class='ruff'>Finally we read the data and plot it as usual:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# get data dict
set data [$circuit getDataDict]
set timeList [dget $data time]
set v9List [dget $data v(9)]
set v10List [dget $data v(10)]
set v11List [dget $data v(11)]
set v12List [dget $data v(12)]

foreach time $timeList v9 $v9List v10 $v10List v11 $v11List v12 $v12List {
    lappend timeV9 [list $time $v9]
    lappend timeV10 [list $time $v10]
    lappend timeV11 [list $time $v11]
    lappend timeV12 [list $time $v12]
}
# plot data with ticklecharts
set nodes [list 9 10 11 12]
set layout [ticklecharts::Gridlayout new]
set i 0
foreach node $nodes {
    ticklecharts::chart create chartV$node
    chartV$node SetOptions -title {} -tooltip {} -animation &quot;False&quot; -toolbox {feature {dataZoom {yAxisIndex &quot;none&quot;}}}
    chartV$node Xaxis -name &quot;time, s&quot; -minorTick {show &quot;True&quot;} -type &quot;value&quot;
    chartV$node Yaxis -name &quot;v(${node}), V&quot; -minorTick {show &quot;True&quot;} -type &quot;value&quot;
    chartV$node Add &quot;lineSeries&quot; -data [subst $[subst timeV$node]] -showAllSymbol &quot;nothing&quot; -name &quot;V(${node})&quot; -symbolSize &quot;0&quot;
    $layout Add chartV$node -bottom &quot;[= {4+24*$i}]%&quot; -height &quot;18%&quot; -width &quot;80%&quot;
    incr i
}

set fbasename [file rootname [file tail [info script]]]
$layout Render -outfile [file normalize [file join .. html_charts $fbasename.html]] -height 800px
</pre>

</figure><p class='ruff'>Results:</p>
<p class='ruff'><img src="assets/img/four_bit_adder_wvfm.png" alt="fourbit_adder" /></p>
<h2 class='ruff'><a name='::Tutorials-S-parameter simulation of pass-band filter'></a>S-parameter simulation of pass-band filter<span class='ruff-uplink'><a href='#top'>Top</a>, <a href='index.html'>Main</a>, <a href='index-docindex.html#'>Index</a></span></h2>
<div style='clear:both;'></div>
<p class='ruff'>This example demonstrates capability of AC simulation with S-parameter output in Ngspice. As the circuit of interest we use band pass filter sourced from Ngspice examples (/examples/sp/filter.sp).</p>
<p class='ruff'>The circuit image is (from Novarianti, Dini. (2019). Design and Implementation of Chebyshev Band Pass Filter with M-Derived Section in Frequency Band 88 - 108 MHz):</p>
<p class='ruff'><img src="assets/img/band_pass_filter.png" alt="filter" /></p>
<p class='ruff'>The circuit is built with following code:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# create top-level circuit
set circuit [Circuit new {filter s-parameters}]
# add elements to circuit
$circuit add [Vport new gen 1 0 -dc 0 -ac 1 -portnum 1]
$circuit add [L new 1 1 2 -l 0.058u]
$circuit add [C new 2 2 0 -c 40.84p]
$circuit add [L new 3 2 3 -l 0.128u]
$circuit add [C new 4 3 0 -c 47.91p]
$circuit add [L new 5 3 4 -l 0.128u]
$circuit add [C new 6 4 0 -c 40.48p]
$circuit add [L new 7 4 5 -l 0.058u]
$circuit add [L new a 5 6 -l 0.044u]
$circuit add [L new b 6 a -l 0.078u]
$circuit add [C new b a 0 -c 17.61p]
$circuit add [L new c 6 b -l 0.151u]
$circuit add [C new c b 0 -c 34.12p]
$circuit add [C new 7 6 7 -c 26.035p]
$circuit add [L new 8 7 0 -l 0.0653u]
$circuit add [C new 8 7 8 -c 20.8p]
$circuit add [L new 9 8 0 -l 0.055u]
$circuit add [C new 9 8 9 -c 20.8p]
$circuit add [L new 10 9 0 -l 0.653u]
$circuit add [C new 10 9 out -c 45.64p]
$circuit add [Vport new l out 0 -dc 0 -ac 0 -portnum 2]
</pre>

</figure><p class='ruff'>You should draw attention to special voltage sources, <a href="index-SpiceGenTcl-Ngspice-Sources.html#::SpiceGenTcl::Ngspice::Sources::Vport" title="::SpiceGenTcl::Ngspice::Sources::Vport" class='ruff_cmd'>::SpiceGenTcl::Ngspice::Sources::Vport</a>, that represents RF-port with special parameter <code>-portnum</code> as a port number, so digits in S-parameters are refered to these port numbers (i.e. S11, S12, S21, S22).</p>
<p class='ruff'>Special analysis object for S-parameter simulation is <a href="index-SpiceGenTcl-Ngspice-Analyses.html#::SpiceGenTcl::Ngspice::Analyses::Sp" title="::SpiceGenTcl::Ngspice::Analyses::Sp" class='ruff_cmd'>::SpiceGenTcl::Ngspice::Analyses::Sp</a>, parameters are the same as for AC analysis:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
$circuit add [Sp new -variation lin -n 500 -fstart 10meg -fstop 200meg]
</pre>

</figure><p class='ruff'>As usual, we create simulator, run the circuit and read the data:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
set simulator [BatchLiveLog new {batch1}]
# attach simulator object to circuit
$circuit configure -Simulator $simulator
$circuit runAndRead
# get data object
set data [$circuit getDataDict]
</pre>

</figure><p class='ruff'>Next few lines of code is used to extract magnitude of S11 and S21 parameters:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
# get frequency
set freq [dget $data frequency]
# get s11
set s11 [dget $data v(s_1_1)]
# get s21
set s21 [dget $data v(s_2_1)]
# extract real/imaginary parts of S11 and S21, and calculate magnitude
set freq [lmap val $freq {@ $val 0}]
set s11Mag [lmap s11Re [lmap val $s11 {@ $val 0}] s11Im [lmap val $s11 {@ $val 1}] {= {sqrt($s11Re**2+$s11Im**2)}}]
set s21Mag [lmap s21Re [lmap val $s21 {@ $val 0}] s21Im [lmap val $s21 {@ $val 1}] {= {sqrt($s21Re**2+$s21Im**2)}}]
# prepare data for ticklecharts
set freq_s11Mag [lmap freqVal $freq s11MagVal $s11Mag {list $freqVal $s11MagVal}]
set freq_s21Mag [lmap freqVal $freq s21MagVal $s21Mag {list $freqVal $s21MagVal}]
</pre>

</figure><p class='ruff'>Now we are ready to plot data:</p>

<figure  class='ruff-snippet ruff-figure'><pre class='ruff'>
set chart [ticklecharts::chart new]
$chart Xaxis -name &quot;Frequency, Hz&quot; -minorTick {show &quot;True&quot;} -type &quot;value&quot;
$chart Yaxis -name &quot;mag(S)&quot; -minorTick {show &quot;True&quot;} -type &quot;value&quot;
$chart SetOptions -title {} -tooltip {} -legend {} -animation &quot;False&quot; -toolbox {feature {dataZoom {yAxisIndex &quot;none&quot;}}}
$chart Add &quot;lineSeries&quot; -data $freq_s11Mag -showAllSymbol &quot;nothing&quot; -name &quot;S11&quot; -symbolSize &quot;0&quot;
$chart Add &quot;lineSeries&quot; -data $freq_s21Mag -showAllSymbol &quot;nothing&quot; -name &quot;S21&quot; -symbolSize &quot;0&quot;
set fbasename [file rootname [file tail [info script]]]

$chart Render -outfile [file normalize [file join .. html_charts $fbasename.html]]
</pre>

</figure><p class='ruff'>The result is:</p>
<p class='ruff'><img src="assets/img/filter_results.png" alt="filter" /></p>
<p class='ruff'>We can clearly see the band that spans from 80Mhz to 128Mhz.</p>
</main><nav class='ruff-nav'><ul ><li class='ruff-toc1'><a style='padding-top:2px;' href='index.html'>Start page</a></li>
<li class='ruff-toc1'><a href='index-docindex.html' accesskey='i'>Index</a></li>
<hr>
<li class='ruff-toc1'><a href='index-List-of-devices.html'>List of devices</a></li>
<li class='ruff-toc1'><a href='index-FAQ.html'>FAQ</a></li>
<li class='ruff-toc1'><a class='ruff-highlight' href='index-Tutorials.html'>Tutorials</a></li>
<li class='ruff-toc1'><a href='index-Tips.html'>Tips</a></li>
<li class='ruff-toc1'><a href='index-Advanced.html'>Advanced</a></li>
<li class='ruff-toc1'><a href='index-SpiceGenTcl.html'>SpiceGenTcl</a></li>
<li class='ruff-toc1'><a href='index-SpiceGenTcl-Common-BasicDevices.html'>SpiceGenTcl::Common::BasicDevices</a></li>
<li class='ruff-toc1'><a href='index-SpiceGenTcl-Common-Sources.html'>SpiceGenTcl::Common::Sources</a></li>
<li class='ruff-toc1'><a href='index-SpiceGenTcl-Ngspice-BasicDevices.html'>SpiceGenTcl::Ngspice::BasicDevices</a></li>
<li class='ruff-toc1'><a href='index-SpiceGenTcl-Ngspice-Sources.html'>SpiceGenTcl::Ngspice::Sources</a></li>
<li class='ruff-toc1'><a href='index-SpiceGenTcl-Ngspice-SemiconductorDevices.html'>SpiceGenTcl::Ngspice::SemiconductorDevices</a></li>
<li class='ruff-toc1'><a href='index-SpiceGenTcl-Ngspice-Analyses.html'>SpiceGenTcl::Ngspice::Analyses</a></li>
<li class='ruff-toc1'><a href='index-SpiceGenTcl-Ngspice-Simulators.html'>SpiceGenTcl::Ngspice::Simulators</a></li>
<li class='ruff-toc1'><a href='index-SpiceGenTcl-Xyce-BasicDevices.html'>SpiceGenTcl::Xyce::BasicDevices</a></li>
<li class='ruff-toc1'><a href='index-SpiceGenTcl-Xyce-Sources.html'>SpiceGenTcl::Xyce::Sources</a></li>
<li class='ruff-toc1'><a href='index-SpiceGenTcl-Xyce-SemiconductorDevices.html'>SpiceGenTcl::Xyce::SemiconductorDevices</a></li>
<li class='ruff-toc1'><a href='index-SpiceGenTcl-Xyce-Analyses.html'>SpiceGenTcl::Xyce::Analyses</a></li>
<li class='ruff-toc1'><a href='index-SpiceGenTcl-Xyce-Simulators.html'>SpiceGenTcl::Xyce::Simulators</a></li>
<hr><li class='ruff-toc2'><a href='#::Tutorials-Netlist manipulations'>Netlist manipulations</a></li><li class='ruff-toc3'><a href='#::Tutorials-Delete element'>Delete element</a></li><li class='ruff-toc3'><a href='#::Tutorials-Get element'>Get element</a></li><li class='ruff-toc3'><a href='#::Tutorials-Print list of all elements'>Print list of all elements</a></li><li class='ruff-toc3'><a href='#::Tutorials-Temporarly remove of element'>Temporarly remove of element</a></li><li class='ruff-toc2'><a href='#::Tutorials-Subcircuit definition'>Subcircuit definition</a></li><li class='ruff-toc2'><a href='#::Tutorials-Diode current simualtion'>Diode current simualtion</a></li><li class='ruff-toc2'><a href='#::Tutorials-Diode capacitance simualtion'>Diode capacitance simualtion</a></li><li class='ruff-toc2'><a href='#::Tutorials-Sensitive analysis of differential pair'>Sensitive analysis of differential pair</a></li><li class='ruff-toc2'><a href='#::Tutorials-Transient simulation of ring oscillator'>Transient simulation of ring oscillator</a></li><li class='ruff-toc2'><a href='#::Tutorials-Transient simulation of four-bit adder'>Transient simulation of four-bit adder</a></li><li class='ruff-toc2'><a href='#::Tutorials-S-parameter simulation of pass-band filter'>S-parameter simulation of pass-band filter</a></li></ul></nav><footer class='ruff-layout-footer ruff-ft'><div style='float: right;'>Document generated by <a href='https://ruff.magicsplat.com'>Ruff!</a></div><div>&copy; George Yashin</div></footer>
</div></body></html>
